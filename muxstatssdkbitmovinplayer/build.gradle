apply plugin: 'com.android.library'
apply plugin: 'checkstyle'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'com.kezong.fat-aar'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 2
        multiDexEnabled true

        buildConfigField("String", "MUX_PLUGIN_VERSION", "\"${versionName}\"")
        buildConfigField("String", "MUX_PLUGIN_NAME", "\"android-bitmovinplayer-mux\"")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions('api')

    productFlavors {
        r3_11_1 {
            dimension 'api'
        }
        r3_11_1_ads {
            dimension 'api'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    build  {
        doLast {
            productFlavors.each {
                flavor ->
                    def versionName = flavor.name.replaceAll(/_/, ".")
                    if (!versionName.endsWith(".ads")) {
                        copy {
                            from "${buildDir}/outputs/aar/${project.name}-${flavor.name}-release.aar", "${buildDir}/outputs/mapping/${flavor.name}Release/mapping.txt"
                            into "${buildDir}/outputs/artifacts/"
                            rename("${project.name}-${flavor.name}-release.aar", "mux-${versionName}.aar")
                            rename("mapping.txt", "mapping-${versionName}.txt")
                            File aarFile = new File("${buildDir}/outputs/artifacts/mux-${versionName}.aar")
                            println "===++++ >>>>> ${aarFile.name} size = ${aarFile.size()}, exists ${aarFile.exists()}";
                            if ( aarFile.length() > 120000 ) {
                                // Fail
                                throw new GradleException(
                                        "${project.name}-${flavor.name}-release.aar exceed 120KB!");
                            }
                        }
                    }
            }
        }
    }
}

apply from: '../MuxAndroidSDKPublishing/declare_publication.gradle'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api "com.mux:stats.muxcore:7.0.10"
    // This is the version used by Bitmovin. For some reason this must be declared explicitly for buildkite to work
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.bitmovin.player:player:3.11.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
