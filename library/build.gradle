apply plugin: 'com.android.library'
apply plugin: 'checkstyle'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'com.mux.gradle.android.mux-android-distribution'

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 33
        multiDexEnabled true

        buildConfigField("String", "MUX_PLUGIN_VERSION", "\"${versionName}\"")
        buildConfigField("String", "MUX_PLUGIN_NAME", "\"android-bitmovinplayer-mux\"")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions('api')

    productFlavors {
        r3_11_1 {
            dimension 'api'
        }
        r3_11_1_ads {
            dimension 'api'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

muxDistribution {
  devVersion versionFromCommitHash('dev-')
  releaseVersion versionFromTag()
  artifactIds artifactFromFlavorValue('api')
  groupIds just("com.mux.stats.sdk.muxstats")
  publicReleaseIf releaseIfCmdFlag("publicRelease")

  dokkaConfig {
    moduleName = "Mux Data SDK for Bitmovin Player"
    footer = "(c) " + new Date().format("yyyy") + " Mux, Inc. Have questions or need help?" +
        " Contact support@mux.com"
  }

  // TODO: This is kinda clumsy, packageJavadocs should be a function not a property probably
  packageJavadocs =  releaseIfCmdFlag("publicRelease").call()
  publishIf { it.containsIgnoreCase("release") }
  artifactoryConfig {
    contextUrl = "https://muxinc.jfrog.io/artifactory/"
    releaseRepoKey = 'default-maven-release-local'
    devRepoKey = 'default-maven-local'
  }
}

dependencies {
    api 'com.mux.stats.sdk.muxstats:android:1.3.1'
    // This is the version used by Bitmovin. For some reason this must be declared explicitly for buildkite to work
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.bitmovin.player:player:3.11.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}
