apply plugin: 'com.android.application'

android {
    compileSdkVersion project.ext.compileSdkVersion

    defaultConfig {
        multiDexEnabled true
        applicationId "com.mux.stats.sdk.muxstats.bitmovinplayer.demo"
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        r3_11_1 {
            java.srcDirs = ['./src/no_ads/java']
            res.srcDirs = ['./src/no_ads/res']
            assets.srcDirs = ['./src/no_ads/assets']
        }
        r3_11_1_ads {
            java.srcDirs = ['./src/ads/java']
            res.srcDirs = ['./src/ads/res']
            assets.srcDirs = ['./src/ads/assets']
        }
    }

    flavorDimensions('api')

    productFlavors {
        r3_11_1 {
            dimension 'api'
        }
        r3_11_1_ads {
            dimension 'api'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.bitmovin.player:player:3.11.1'
    // This is the version used by Bitmovin. For some reason this must be declared explicitly for buildkite to work
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

afterEvaluate {
    dependencies {
        r3_11_1Api 'com.mux.stats.sdk.muxstats:muxstatssdkbitmovinplayer_r3_11_1:'+project.ext.versionName
        r3_11_1_adsApi 'com.mux.stats.sdk.muxstats:muxstatssdkbitmovinplayer_r3_11_1:'+project.ext.versionName
    }
}